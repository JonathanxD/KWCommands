plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'me.champeau.gradle.jmh' version '0.5.2'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

group 'com.github.jonathanxd'
version '1.6.20'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'license'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

license {
    header rootProject.file("LICENSE_HEADER")
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    ext.iutils_version = '4.16.5-1'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "com.googlecode.json-simple:json-simple:1.1.1"

    compile "com.github.JonathanxD.JwIUtils:JwIUtils:$iutils_version"
    compile "com.github.JonathanxD.JwIUtils:jwiutils-kt:$iutils_version"
    compile "com.github.JonathanxD.JwIUtils:json-lang-loader:$iutils_version"
    compile "com.github.JonathanxD.JwIUtils:links:$iutils_version"

    jmh "org.openjdk.jmh:jmh-core:1.19"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.19"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.json"
    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    exclude "**/*.lang"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

